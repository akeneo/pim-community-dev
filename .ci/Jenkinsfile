#!groovy
@Library("k8s-utils@2.x")

String[] features = ["features", "vendor/akeneo/pim-community-dev/features"]
String ceBranch = "2.0"
String ceOwner = "akeneo"
String launchUnitTests = "yes"
String launchIntegrationTests = "yes"
String launchEndToEndTests = "yes"
String verboseOutputs = "yes"
String slackChannel = ""
String tag = "eu.gcr.io/akeneo-ci/pim-enterprise-dev:${env.BRANCH_NAME}"

stage("Build") {
    milestone 1
    if (env.BRANCH_NAME =~ /^PR-/) {
        timeout(time:5, unit:'DAYS') {
            userInput = input(message: 'Launch tests?', parameters: [
                string(defaultValue: '2.0', description: 'Community Edition branch used for the build', name: 'ce_branch'),
                string(defaultValue: 'akeneo', description: 'Owner of the repository on GitHub', name: 'ce_owner'),
                choice(choices: 'yes\nno', description: 'Run unit tests and code style checks', name: 'launchUnitTests'),
                choice(choices: 'yes\nno', description: 'Run integration tests', name: 'launchIntegrationTests'),
                choice(choices: 'yes\nno', description: 'Run end to end tests (whole Legacy Behat suite)', name: 'launchEndToEndTests'),
                string(defaultValue: 'features,vendor/akeneo/pim-community-dev/features', description: 'Legacy end to end scenarios to run', name: 'features'),
                choice(choices: 'no\nyes', description: 'Enable Verbose mode', name: 'verboseOutputs'),
                string(defaultValue: '', description: 'Channel or user to notify (example : "#channel,@user")', name: 'slackChannel'),
            ])

            ceBranch = userInput['ce_branch']
            ceOwner = userInput['ce_owner']
            features = userInput['features'].tokenize(',')
            launchUnitTests = userInput['launchUnitTests']
            launchIntegrationTests = userInput['launchIntegrationTests']
            launchEndToEndTests = userInput['launchEndToEndTests']
            verboseOutputs = userInput['verboseOutputs']
            slackChannel = userInput['slackChannel']
        }
    }
    milestone 2

    pod {
        container('docker') {
            checkout scm

            dir('packages/pim-community-dev') {
                checkout([$class: 'GitSCM',
                    branches: [[name: ceBranch]],
                    userRemoteConfigs: [[credentialsId: "github-credentials", url: "https://github.com/${ceOwner}/pim-community-dev.git"]]
                ])
            }

            sh "cp .ci/Dockerfile Dockerfile"
            sh "gcloud builds submit --config .ci/builder.yaml --substitutions _IMAGE_TAG=${tag} ."
        }
    }
}

stage("Test") {
    try {
        parallel(
            "php-coupling-detector": {testif(
                condition: launchUnitTests.equals("yes"),
                container: tag,
                cpuRequest: "200m",
                script: "cd /var/www/pim && vendor/bin/php-coupling-detector detect --config-file=.php_cd.php src"
            )},
            "phpspec": {testif(
                condition: launchUnitTests.equals("yes"),
                container: tag,
                cpuRequest: "200m",
                script: "cd /var/www/pim && su -s /bin/sh www-data -c './vendor/bin/phpspec run --no-interaction --format=junit' > %workspace%/junit_output.xml",
                junit: "junit_output.xml"
            )},
            "php-cs-fixer": {testif(
                condition: launchUnitTests.equals("yes"),
                container: tag,
                cpuRequest: "200m",
                script: "cd /var/www/pim && vendor/bin/php-cs-fixer fix --diff --dry-run --config=.php_cs.php --format=junit > %workspace%/junit_output.xml",
                junit: "junit_output.xml"
            )},
            "grunt": {testif(
                condition: launchUnitTests.equals("yes"),
                container: tag,
                cpuRequest: "200m",
                script: "cd /var/www/pim && yarn run lint"
            )},
            "integration": {queue(
                condition: launchIntegrationTests.equals("yes"),
                container: tag,
                containers: pimContainers(image: tag, selenium: false),
                verbose: (verboseOutputs == "yes"),
                fetcher: {return pimPhpunitFetcher(paths:["/var/www/pim/src", "/var/www/pim/vendor/akeneo/pim-community-dev/src", "/var/www/pim/vendor/akeneo/pim-community-dev/tests"])},
                parallelism: 50
            )},
            "behat": {queue(
                condition: launchEndToEndTests.equals("yes"),
                container: tag,
                containers: pimContainers(image: tag),
                verbose: (verboseOutputs == "yes"),
                fetcher: {return pimBehatFetcher(hasToInstall: true, script: "cd /var/www/pim/ && vendor/bin/behat -p default --list-scenarios", features: features)}
            )}
        )
    } finally {
        notifySlack(slackChannel)
    }
}
